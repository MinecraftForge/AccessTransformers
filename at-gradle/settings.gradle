import org.gradle.api.initialization.resolve.RepositoriesMode

plugins {
    id 'dev.gradleplugins.gradle-plugin-development' version '1.9.0'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.10.0'
}

rootProject.name = 'at-gradle'

includeBuild '..'

dependencyResolutionManagement {
    // Repositories are located in build.gradle for this project
    // dev.gradleplugins.groovy-gradle-plugin is bugged and force adds repositories on the project
    // so, we can't declare the repositories in here
    repositoriesMode = RepositoriesMode.PREFER_PROJECT

    versionCatalogs.register('libs') {
        plugin 'licenser', 'net.minecraftforge.licenser' version '1.2.0'
        plugin 'gradleutils', 'net.minecraftforge.gradleutils' version '2.6.0'
        plugin 'javadoc-links', 'io.freefair.javadoc-links' version '8.13.1'
        plugin 'plugin-publish', 'com.gradle.plugin-publish' version '1.3.1'
        plugin 'shadow', 'com.gradleup.shadow' version '9.0.0-beta13'

        // Gradle API
        version 'gradle', '7.3'

        // Utils
        library 'utils-hash',     'net.minecraftforge', 'hash-utils'     version '0.1.9'
        library 'utils-download', 'net.minecraftforge', 'download-utils' version '0.3.1'
        bundle 'utils', ['utils-hash', 'utils-download']

        // Static Analysis
        library 'nulls', 'org.jetbrains', 'annotations' version '26.0.2'
    }
}
