plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    alias libs.plugins.licenser
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
    withSourcesJar()
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

tasks.named('test', Test) {
    useJUnitPlatform()
    reports.html.outputLocation = rootProject.layout.buildDirectory.dir('reports')
    reports.junitXml.outputLocation = rootProject.layout.buildDirectory.dir('test-results')
}

dependencies {
    implementation libs.modlauncher
    implementation rootProject
    testImplementation testLibs.junit.api
    testRuntimeOnly testLibs.bundles.junit.runtime
}

idea.module { downloadSources = downloadJavadoc = true }

// Hack eclipse into knowing that the gradle deps are modules
eclipse.classpath {
    downloadSources = downloadJavadoc = true
    containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
    file.whenMerged { entries.findAll { it.kind == 'lib' || it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each { it.entryAttributes['module'] = 'true' } }
}