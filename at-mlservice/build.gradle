plugins {
    id 'eclipse'
    id 'java-library'
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4)'
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
    mavenLocal()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
    withSourcesJar()
}

license {
    header = rootProject.file("LICENSE-header.txt")
    newLine = false
}

test {
    useJUnitPlatform()
    reports.html.destination = rootProject.file("build/reports/")
    reports.junitXml.destination = rootProject.file("build/test-results/")
}

dependencies {
    implementation(libs.modlauncher)
    implementation(rootProject)
    testImplementation(libs.junit.api)
    testRuntimeOnly(libs.bundles.junit.runtime)
}

// Hack eclipse into knowing that the gradle deps are modules
eclipse.classpath {
    containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
    file.whenMerged { entries.findAll { it.kind == 'lib' || it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each { it.entryAttributes['module'] = 'true' } }
}