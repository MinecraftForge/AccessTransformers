plugins {
    id 'eclipse'
    id 'java-library'
    id 'net.minecraftforge.licenser'
    id 'net.minecraftforge.gradleutils'
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
    mavenLocal()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
}

license {
    header = rootProject.file("LICENSE-header.txt")
    newLine = false
}

test {
    useJUnitPlatform()
    reports.html.destination = rootProject.file("build/reports/")
    reports.junitXml.destination = rootProject.file("build/test-results/")
}

dependencies {
    testImplementation(rootProject)
    testImplementation(project(':at-mlservice'))
    testImplementation(project(':at-test-jar'))
    testImplementation(libs.junit.api)
    testImplementation(libs.unsafe)
    testImplementation(libs.securemodules)
    testImplementation(libs.modlauncher)
    testImplementation(libs.asm)
    testImplementation(libs.asm.tree)
    testImplementation(libs.jopt.simple)
    testImplementation(libs.log4j.api)
    testImplementation(libs.log4j.core)
    testImplementation(libs.gson)
    testRuntimeOnly(libs.bundles.junit.runtime)
    testCompileOnly(libs.nulls)
}

tasks.register('testAll', AggregateTest) {
    input = file('build/test-results/')
    output = rootProject.file('test_results.html')
}

VALID_VMS.each { javaVendor, javaVersions ->
    javaVersions.each { javaVersion ->
        def output = file("build/test-results/${javaVendor}-${javaVersion}/")
        output.mkdirs()
        def task = tasks.register("test${javaVendor}${javaVersion}", Test) {
            useJUnitPlatform()
            javaLauncher.set(javaToolchains.launcherFor {
                it.vendor.set(JvmVendorSpec."${javaVendor.toUpperCase(Locale.ROOT)}" as JvmVendorSpec)
                it.languageVersion.set(JavaLanguageVersion.of(javaVersion))
                it.implementation.set(JvmImplementation.VENDOR_SPECIFIC)
            })
            reports.html.destination = file("build/test-reports/${javaVendor}-${javaVersion}/")
            reports.junitXml.destination = output
        }
        testAll.inputs.dir(output)
        testAll.dependsOn(task)
        testAll.mustRunAfter(task)
    }
}