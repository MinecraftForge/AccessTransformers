configurations {
    jmhOnly
}

dependencies {
    implementation(rootProject)
    implementation('org.openjdk.jmh:jmh-core:1.35')
    jmhOnly('org.openjdk.jmh:jmh-core:1.35')
    jmhOnly('org.openjdk.jmh:jmh-generator-annprocess:1.35')
    jmhOnly(sourceSets.main.output)
    annotationProcessor('org.openjdk.jmh:jmh-generator-annprocess:1.35')
    implementation('org.ow2.asm:asm:9.3')
}

task jmh(type: JavaExec, dependsOn: sourceSets.main.output) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    jvmArgs= [
        '-p', sourceSets.main.runtimeClasspath.asPath,
        '--add-modules', 'ALL-MODULE-PATH',
    ]
    classpath = files(configurations.jmhOnly.asPath)
    mainClass = 'org.openjdk.jmh.Main'
    args    '-bm', 'avgt'  // benchmark mode
    args    '-r', '5s' // iteration time
    args    '-w', '5s' // warmup time
    args    '-wi', '2' // warmup iterations
    args    '-prof', 'stack'
    args    '-prof', 'jfr' // profilers
    args    '-tu', 'us' // time unit
    args    '-i', '2' // iterations
    args    '-f', '1' // forks
    args    '-rff', project.file("${rootProject.buildDir}/jmh_results.txt")  // results file
    args    'net.minecraftforge.accesstransformer.benchmarks.AccessTransformerListBenchmark'
}