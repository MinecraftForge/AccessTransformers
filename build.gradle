plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'antlr'
    id 'org.javamodularity.moduleplugin' version '1.8.3' apply false
    id 'net.minecraftforge.gradleutils' version '2.+'
}

apply plugin: 'maven-publish'

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'org.javamodularity.moduleplugin'

    group 'net.minecraftforge'

    java.toolchain.languageVersion = JavaLanguageVersion.of(16)

    version = gradleutils.getTagOffsetVersion()

    repositories {
        mavenLocal()
        maven {
            name = 'forge'
            url = 'https://maven.minecraftforge.net/'
        }
    }

    def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { type -> version.toUpperCase().contains(type) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
    }

    dependencyUpdates {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }


    // Hack eclipse into knowing that the gradle deps are modules
    eclipse {
        classpath {
            containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
            file {
                whenMerged {
                    entries.findAll { it.kind == 'con' && it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each {
                        it.entryAttributes['module'] = 'true'
                    }
                }
            }
        }
    }
}
println('Version: ' + version)

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

sourceSets {
    testJars
    modlauncherImpl
}

compileJava {
    dependsOn generateGrammarSource
    targetCompatibility = sourceCompatibility = 1.8
}

jar.manifest = manifest {
    attributes(['Specification-Title': 'accesstransformers',
                'Specification-Vendor': 'forge',
                'Specification-Version': '1', // Currently version 1 of the accesstransformer specification
                'Implementation-Title': project.name,
                'Implementation-Version': "${project.version}+${System.getenv('BUILD_NUMBER')?:0}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
                'Implementation-Vendor': 'forge',
                'Implementation-Timestamp': java.time.Instant.now().toString(),
                'Git-Commit': gradleutils.gitInfo.abbreviatedId,
                'Git-Branch': gradleutils.gitInfo.branch,
                'Build-Number': "${System.getenv('BUILD_NUMBER')?:0}" ],
            'net/minecraftforge/accesstransformer/service/')
    attributes(['Specification-Title': 'accesstransformers',
                'Specification-Vendor': 'Forge',
                'Specification-Version': '1', // Currently version 1 of the accesstransformer specification
                'Implementation-Title': project.name,
                'Implementation-Version': "${project.version}+${System.getenv('BUILD_NUMBER')?:0}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
                'Implementation-Vendor': 'forge',
                'Implementation-Timestamp': java.time.Instant.now().toString(),
                'Git-Commit': gradleutils.gitInfo.abbreviatedId,
                'Git-Branch': gradleutils.gitInfo.branch,
                'Build-Number': "${System.getenv('BUILD_NUMBER')?:0}" ],
            'net/minecraftforge/accesstransformer/')
}

task testsJar(type: Jar) {
    archiveClassifier = 'testsjar'
    from sourceSets.testJars.output
}

task sourcesJar(type: Jar) {
    dependsOn generateGrammarSource
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

changelog {
    fromTag '1.0'
}

build.dependsOn testsJar

ext.asmVersion = 9.3
dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.+')
    testImplementation('org.powermock:powermock-core:2.0+')
    testImplementation('cpw.mods:modlauncher:9.0.4')
    testImplementation('com.google.code.gson:gson:2.8.9')
    testImplementation(sourceSets.modlauncherImpl.output)
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.8.+')
    testRuntimeOnly(sourceSets.testJars.runtimeClasspath)
    implementation('org.antlr:antlr4-runtime:4.9.1')
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4')
    implementation("org.ow2.asm:asm:${asmVersion}")
    implementation("org.ow2.asm:asm-commons:${asmVersion}")
    implementation("org.ow2.asm:asm-tree:${asmVersion}")
    implementation('org.apache.logging.log4j:log4j-api:2.17.0')
    implementation('org.apache.logging.log4j:log4j-core:2.17.0')
    modlauncherImplImplementation('cpw.mods:modlauncher:9.0.4')
    modlauncherImplImplementation(sourceSets.main.output)
    antlr('org.antlr:antlr4:4.9.1')
}

jar {
    from sourceSets.modlauncherImpl.output
}

shadowJar {
    classifier 'fatjar'
    exclude '**/Log4j2Plugins.dat'
    manifest {
        inheritFrom jar.manifest
        attributes('Main-Class': 'net.minecraftforge.accesstransformer.TransformerProcessor')
        attributes('Multi-Release': 'true')
    }
    dependencies {
        exclude(dependency {
            it.moduleName == 'antlr4'
        })
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives testsJar
    archives shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                name = 'Access Transformers'
                description = 'Transforms class member access based on specification files'
                url = 'https://github.com/MinecraftForge/AccessTransformers'
                scm {
                    url = 'https://github.com/MinecraftForge/AccessTransformers'
                    connection = 'scm:git:git://github.com/MinecraftForge/AccessTransformers.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/AccessTransformers.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/AccessTransformers/issues'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'DemonWav'
                        name = 'DemonWav'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}

generateGrammarSource {
    arguments += ['-visitor', '-package', 'net.minecraftforge.accesstransformer.generated']
    outputDirectory = file("${project.buildDir}/generated-src/antlr/main/net/minecraftforge/accesstransformer/generated/")
}
