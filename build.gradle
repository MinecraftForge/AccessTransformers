import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'io.freefair.javadoc-links'
    id 'net.minecraftforge.gradleutils'
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
    alias libs.plugins.versions
    alias libs.plugins.shadow
    alias libs.plugins.licenser
}

final projectDisplayName = 'AccessTransformers'
final projectVendor = 'Forge Development LLC'
final projectArtifactId = base.archivesName = 'accesstransformers'
description = 'Transforms class member access based on specification files.'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

configurations {
    register('include') {
        canBeDeclared = true
        canBeResolved = true
    }
}

dependencies {
    implementation libs.jopt
    implementation libs.bundles.asm
    implementation libs.log4j.api
    implementation libs.log4j.core
    include(project(':at-mlservice')) { transitive = false }
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    final map = [
        'Specification-Title'   : projectDisplayName,
        'Specification-Vendor'  : projectVendor,
        'Specification-Version' : '1',
        'Implementation-Title'  : projectDisplayName,
        'Implementation-Version': project.version,
        'Implementation-Vendor' : projectVendor
    ]

    manifest {
        attributes(map, 'net/minecraftforge/accesstransformer/')
        attributes(map, 'net/minecraftforge/accesstransformer/service/')
    }

    dependsOn configurations.named('include')
    from configurations.named('include').map { it.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } }
}

tasks.named('shadowJar', ShadowJar) {
    manifest {
        attributes([
            'Main-Class'   : 'net.minecraftforge.accesstransformer.TransformerProcessor',
            'Multi-Release': 'true'
        ])
    }

    archiveClassifier = 'fatjar'
    exclude '**/Log4j2Plugins.dat'
}

tasks.withType(Javadoc).configureEach {
    options { StandardJavadocDocletOptions options ->
        options.windowTitle = projectDisplayName + project.version
        options.tags 'apiNote:a:API Note:', 'implNote:a:Implementation Note:', 'implSpec:a:Implementation Requirements:'
    }
}

changelog {
    from '1.0'
    publishAll = true
}

artifacts {
    archives shadowJar
}

publishing {
    repositories {
        maven gradleutils.publishingForgeMaven
    }

    publications.register('mavenJava', MavenPublication) {
        from components.java

        artifactId = projectArtifactId
        gradleutils.promote(it)

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.MIT
            }

            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.cpw
                developer gradleutils.pom.developers.DemonWav
            }
        }
    }
}

idea.module { downloadSources = downloadJavadoc = true }
eclipse.classpath { downloadSources = downloadJavadoc = true }
