import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.versions
    alias libs.plugins.gradleutils
    alias libs.plugins.gitversion
    alias libs.plugins.changelog
    alias libs.plugins.shadow
    alias libs.plugins.licenser
}

gradleutils.displayName = 'AccessTransformers'
final projectVendor = 'Forge Development LLC'
base.archivesName = 'accesstransformers'
description = 'Transforms class member access based on specification files.'
group = 'net.minecraftforge'
version = gitversion.tagOffset

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

configurations {
    register('include') {
        canBeDeclared = true
        canBeResolved = true
    }
}

dependencies {
    implementation libs.jopt
    implementation libs.bundles.asm
    implementation libs.log4j.api
    implementation libs.log4j.core
    include(project(':at-mlservice')) { transitive = false }
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    final map = [
        'Specification-Title'   : gradleutils.displayName,
        'Specification-Vendor'  : projectVendor,
        'Specification-Version' : '1',
        'Implementation-Title'  : gradleutils.displayName,
        'Implementation-Version': project.version,
        'Implementation-Vendor' : projectVendor
    ]

    manifest {
        attributes(map, 'net/minecraftforge/accesstransformer/')
        attributes(map, 'net/minecraftforge/accesstransformer/service/')
    }

    dependsOn configurations.named('include')
    from configurations.named('include').map { it.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } }
}

tasks.named('shadowJar', ShadowJar) {
    manifest {
        attributes([
            'Main-Class'   : 'net.minecraftforge.accesstransformer.TransformerProcessor',
            'Multi-Release': 'true'
        ])
    }

    archiveClassifier = 'fatjar'
    exclude '**/Log4j2Plugins.dat'
}

changelog {
    from '1.0'
    publishAll = true
}

artifacts {
    archives shadowJar
}

publishing {
    repositories {
        maven gradleutils.publishingForgeMaven
    }

    publications.register('mavenJava', MavenPublication) {
        from components.java

        gradleutils.promote(it)

        pom { pom ->
            name = gradleutils.displayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.MIT
            }

            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.cpw
                developer gradleutils.pom.developers.DemonWav
            }
        }
    }
}
