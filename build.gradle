import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'antlr'
    id 'net.minecraftforge.gradleutils' version '[2.1.2,)'
}

group 'net.minecraftforge'

version = gradleutils.tagOffsetVersion
println('Version: ' + version)

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

compileJava {
    dependsOn generateGrammarSource
}

tasks.named('jar', Jar).configure {
    dependsOn ':at-mlservice:processResources'
    dependsOn ':at-mlservice:compileJava'
    doFirst {
        from project(':at-mlservice').sourceSets.main.output
    }

    ext.SPEC = '1' // Should be tag but whatever
    manifest {
        attributes([
            'Specification-Title': 'accesstransformers',
            'Specification-Vendor': 'Forge Development LLC',
            'Specification-Version': SPEC,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'Forge Development LLC'
        ] as java.util.LinkedHashMap, 'net/minecraftforge/accesstransformer/')
        attributes([
            'Specification-Title': 'accesstransformers',
            'Specification-Vendor': 'Forge Development LLC',
            'Specification-Version': SPEC,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'Forge Development LLC'
        ] as java.util.LinkedHashMap, 'net/minecraftforge/accesstransformer/service/')
    }
}

tasks.named('sourcesJar').configure {
    dependsOn generateGrammarSource
}

changelog {
    fromTag '1.0'
}

dependencies {
    implementation(libs.antlr4.runtime)
    implementation(libs.jopt.simple)
    implementation(libs.bundles.asm)
    implementation(libs.log4j.api)
    implementation(libs.log4j.core)
    antlr(libs.antlr4)
}

tasks.named('shadowJar', ShadowJar).configure {
    archiveClassifier = 'fatjar'
    exclude '**/Log4j2Plugins.dat'
    manifest {
        inheritFrom jar.manifest
        attributes([
            'Main-Class': 'net.minecraftforge.accesstransformer.TransformerProcessor',
            'Multi-Release': 'true'
        ])
    }
    dependencies {
        exclude(dependency {
            it.moduleName == 'antlr4'
        })
    }
}

artifacts {
    archives shadowJar
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        artifactId = 'accesstransformers'
        pom {
            name = 'Access Transformers'
            description = 'Transforms class member access based on specification files'
            url = 'https://github.com/MinecraftForge/AccessTransformers'
            PomUtils.setGitHubDetails(pom, 'AccessTransformers')

            license PomUtils.Licenses.MIT

            developers {
                developer PomUtils.Developers.LexManos
                developer PomUtils.Developers.cpw
                developer PomUtils.Developers.DemonWav
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

generateGrammarSource {
    arguments += ['-visitor', '-package', 'net.minecraftforge.accesstransformer.generated']
    outputDirectory = file("${project.buildDir}/generated-src/antlr/main/net/minecraftforge/accesstransformer/generated/")
}

allprojects {
    ext.VALID_VMS = [
        'Adoptium':  [16, 17, 18, 19, 20, 21],
        'Amazon':    [16, 17, 18, 19, 20, 21],
        'Azul':      (16..21),
        'BellSoft':  (16..21),
        'Graal_VM':  [16, 17,     19, 20, 21],
        'IBM':       [16, 17, 18, 19, 20    ],
        'Microsoft': [16, 17,             21],
        'Oracle':    (16..21),
        'SAP':       (16..20)
    ]
    ext.VALID_VMS = [ 'Adoptium':  [16] ]
}
